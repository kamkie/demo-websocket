def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

buildscript {
    ext {
        kotlin_version = '1.0.3'
        springBootVersion = '1.4.0.RELEASE'
        tyrusVersion = '1.13'
        rxJavaVersion = '1.1.1'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    group 'net.devopssolutions'
    version getVersionName()

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: "kotlin"
    apply plugin: 'pmd'
    apply plugin: 'findbugs'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        testCompile group: 'junit', name: 'junit', version: '4.11'
    }
}

project(':ws-client') {
    apply plugin: 'spring-boot'

    configurations {
        compile.exclude module: "spring-boot-starter-tomcat"
    }

    dependencies {
        compile project(':ws-models')

        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.boot:spring-boot-starter-undertow")
        compile("org.springframework.boot:spring-boot-starter-websocket")
        compile("org.springframework.boot:spring-boot-actuator")
//        compile("org.springframework.boot:spring-boot-devtools")
        compile("org.springframework.boot:spring-boot-configuration-processor")
//        compile("com.fasterxml.jackson.core:jackson-databind")
//        compile("org.glassfish.tyrus:tyrus-container-jdk-client:$tyrusVersion")
//        compile("org.glassfish.tyrus.ext:tyrus-client-java8:1.12")
    }
}

project(':ws-server') {
    apply plugin: 'spring-boot'

    configurations {
        compile.exclude module: "spring-boot-starter-tomcat"
    }

    dependencies {
        compile project(':ws-models')

        compile("org.springframework.boot:spring-boot-starter-web")
//        compile("org.springframework.boot:spring-boot-starter-jetty")
        compile("org.springframework.boot:spring-boot-starter-undertow")
        compile("org.springframework.boot:spring-boot-starter-websocket")
        compile("org.springframework.boot:spring-boot-actuator")
//        compile("org.springframework.boot:spring-boot-devtools")
        compile("org.springframework.boot:spring-boot-configuration-processor")

        compile group: 'org.xerial.snappy', name: 'snappy-java', version: '1.1.2.6'
        compile group: 'net.jpountz.lz4', name: 'lz4', version: '1.3.0'
        compile('de.ruedigermoeller:fst:2.47')


//        compile("io.reactivex:rxjava:$rxJavaVersion")
//        compile('io.reactivex:rxjava-async-util:0.21.0')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}
